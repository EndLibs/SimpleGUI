# SimpleGUI
Простой GUI для Processing

- Создание кнопки(прямоугольной / круглой) одной функцией
- Создание слайдера(горизонтального / вертикального) как объекта в классе
- Создание джойстика как объекта в классе
- Создание списка как объекта в классе
- Создание поля ввода текста как объекта в классе


## Содержание
- [Установка](#insl)
- [Использование](#use)
  - [Прямоугольная кнопка](#rb)
  - [Круглая кнопка](#cb)
  - [Горизонтальный слайдер](#xs)
  - [Вертикальный слайдер](#ys)
  - [Джойстик](#js)
  - [Список](#ls)
  - [Текст](#tx)
- [Пример](#exp)
- [Версии](#ver)


<a id="insl"></a>
## Установка
- Скачать архив, распаковать. В получившейся папке лежит пример - файл .pde, в нем лежат все функции и классы


<a id="use"></a>
## Использование
<a id="rb"></a>
### Прямоугольная кнопка
- Является функцией
```cpp
boolean rectButton(float x, float y, float w, float h, float r, color colrect, float ws, color colstrok, String text, float textsz, color coltext);
```
- Возвращает статус кнопки(boolean); нажата - true, не нажата - false
- Аргументы:
- Координаты середины кнопки
  - x(float)
  - y(float)
- Размеры
  - ширина(float)
  - высота(float)
- Радиус скругления углов(float)
- Цвет кнопки(color)
- Толщина обводки(float), если не надо ставим 0
- Цвет обводки(color)
- Текст, который будет написан внутри кнопки(String), если не надо ставим ""
- Размер текста(float)
- Цвет текста(color)


<a id="cb"></a>
### Круглая кнопка
- Является функцией
```cpp
boolean circleButton(float x, float y, float d, color colrect, float ws, color colstrok, String text, float textsz, color coltext);
```
- Все аргументы такие же, как и в прямоугольной кнопке, только вместо ширины и высоты подаётся -
- Диаметр(float)


<a id="xs"></a>
### Горизонтальный слайдер
- Является классом SliderX, при создании ничего не указывается, все аргументы подаются в функцию update(пример sld.update())
```cpp
float update(float x, float y, float w, float h, float r, float wp, float min, float max, boolean pr, color colrect, color colsld, String text, float textsz, color coltext);
```
- Возвращает значение слайдера в заданном диапазоне(float)
- Аргументы:
- Координаты середины слайдера
  - x(float)
  - y(float)
- Размеры
  - Ширина(float)
  - Высота(float)
- Радиус скругления(float)
- Ширина ползунка(float)
- Диапозон значений, который будет принимать слайдер и возвращать
  - минимальное(float)
  - максимальное(float)
- Ступенчатость работы(boolean). Будет ли ползунок принимать столько положений сколько задали выше(так же от результата будет возвращаться только целая часть), или будет принимать неограниченное кол-во положений(результат будет возвращаться в float с дробной частью)
- Цвет слайдера(color)
- Цвет ползунка(color)
- Текст, который будет написан в ползунке(String), если не надо ставим ""
- Размер текста(float)
- Цвет текста(float)


<a id="ys"></a>
### Вертикальный слайдер
- Является классом SliderY, при создании ничего не указывается, все аргументы подаются в функцию update(пример sld.update())
```cpp
float update(float x, float y, float w, float h, float r, float hp, float min, float max, boolean pr, color colrect, color colsld, String text, float textsz, color coltext)
```
- Всё тоже самое, что и в горизонтальном слайдере, только вместо ширины ползунка указывается -
- Высота ползунка(float)


<a id="js"></a>
### Джойстик
- Является классом JoyStick, при создании ничего не указывается, все аргументы подаются в функцию update(пример joy.update())
```cpp
String update(float x, float y, float w, float r, float wp, float xmin, float xmax, float ymin, float ymax, boolean pr, boolean rtc, color colrect, color coljs, String text, float textsz, color coltext)
```
- Возвращает положение джойстика(String) в формате "x,y", разделить строку можно при помощи .split(","), вернет массив из 2 строк(в каждой строке будет находиться значение джойстика)
- Аргументы:
- Координаты середины джойстика
  - x(float)
  - y(float)
- Размер джойстика(float), он квадратный
- Радиус скругления(float)
- Размер стика(float), он квадратный
- Значения для x
  - минимальное(float)
  - максимальное(float)
- Значения для y
  - минимальное(float)
  - максимальное(float)
- Ступенчатость работы(boolean). Как и в слайдерах
- Возврат в центр при отпускании стика(boolean)
- Цвет джойстика(color)
- Цвет стика(color)
- Текст, который будет написан в центре стика(String)
- Размер текста(float)
- Цвет текста(color)


<a id="ls"></a>
### Список
- Является классом List, при создании ничего не указывается, все аргументы подаются в функцию update(пример list.update())
```cpp
int update(float x, float y, float w, float h, String arr[], String text, boolean dlc, float ts, float ts2, color colrect, color coltext)
```
- Возвращает номер выбранной пользователем строки(int)
- Аргументы:
- Координаты середины списка
  - x(float)
  - y(float)
- Размеры
  - Ширина(float)
  - Высота(float)
- Список(String[]), массив  из строк
- Название списка(String)
- Будет ли название списка заменяться на выбранную пользователем строку из списка после выбора(boolean)
- Размер текста названия списка(float)
- Размер текста строк списка(float)
- Цвет списка(color)
- Цвет текста(color)


<a id="tx"></a>
### Текст
- Является классом Text, при создании ничего не указывается, все аргументы подаются в функцию update(пример txt.update())
```cpp
String update(float x, float y, float w, float h, float ts, String text, color colrect, color coltext)
```
- Особенность - **не имеет ограничения текста по длине!** Поле ввода имеет ограниченный размер, а текст нет, если продолжать писать текст, то он на выходе будет целый, а в поле ввода отобразится только часть
- Возвращает введенный текст(String)
- Аргументы:
- Координаты середины поля ввода
  - x(float)
  - y(float)
- Размеры
  - Ширина(float)
  - Высота(float)
- Размер текста(float)
- Текст-заглушка(String), этот текст будет выводится когда в поле ничего не будет
- Цвет поля(color)
- Цвет текста(color)


<a id="exp"></a>
## Пример
- Полный пример со всеми функциями лежит в **архиве**!
```cpp
/*
  Простой GUI для Processing.
 Имеется:
 - Функция-кнопка(прямоугольная, круглая)
 - Класс-слайдер(вертикальный, горизонтальный)
 - Класс-джойстик
 - Класс-список
 - Класс-текст
 by EndPortal
 02.11.2023
 */
boolean flr, flc, fn, fn1;    //Переменные для кнопок
float cx, cy, jx1, jx2, jy1, jy2;    //Переменные для слайдеров и джойстиков
int cbg;    //Переменная выбора списка

SliderX sldx = new SliderX();    //Горизонтальный слайдер

SliderY sldy = new SliderY();    //Вертикальный слайдер

JoyStick js1 = new JoyStick();    //Джойстик

JoyStick js2 = new JoyStick();    //Джойстик

List ls = new List();    //Список

Text tx = new Text();    //Текст

void setup() {
  size(1000, 1000);    //Размер окна
}

void draw() {
  color bg = 0;
  switch(cbg) {    //Выбор цвета фона
  case 0: 
    bg = 255; 
    break;
  case 1: 
    bg = 127; 
    break;
  case 2: 
    bg = #00FF00; 
    break;
  }
  background(fn ? #00FFFF : bg);    //Цвет фона зависит от переменной fn, если она 0, то фон это цвет bg

  String ar[] = new String[3];    //Массив строк для списка
  ar[0] = "Белый";
  ar[1] = "Серый";
  ar[2] = "Лайм";

  //Список(номер строки записывается в cbg); координаты(x, y), размеры(ширина, высота), список значений(массив строк), титульный текст, будет ли титульный текст после первого выбора заменен на выбранное значение, размер титульного текста, размер текста значений, цвет списка, цвет текста;
  cbg = ls.update(450, 100, 200, 50, ar, "Цвет фона", true, 30, 30, #0000FF, 0);

  //Текст (записывается в str); координаты, размер, размер текста, надпись когда в поле ввода ничего нет, цвет поля, цвет текста;
  String str = tx.update(200, height - 200, 300, 200, 26, "Ввод текста", #0000FF, 0);

  println(str);    //Выводим текст в консоль

  //Джойстик; координаты x200, y200, размер 200, скругление углов 25, размер стика 50, значения для x -50.. 50, значения для y -50.. 50, работа стика бесступенчато, возврат в центр отключен, цвет фона, цвет стика,  текст - положение этого стика, размер текста 20, цвет 255 - белый;
  String str2[] = js2.update(200, 200, 200, 25, 50, -50, 50, -50, 50, false, false, #0000FF, #594BFF, str(jx2) + "," + str(jy2), 20, 255).split(",");
  jx2 = float(str2[0]);
  jy2 = float(str2[1]);

  //Джойстик; координаты зависят от положения второго джойстика, размер 200, скругление 20, стик 50, значения для x -50.. 50, для y -50.. 50, стик работает ступенчато, возврат в центр включен, цвет фона, цвет стика, текст - положение этого стика, размер текста 20, цвет 0 - черный;
  String str1[] = js1.update(width - 200 + jx2, height - 200 - jy2, 200, 20, 50, -50, 50, -50, 50, true, true, #0000FF, #594BFF, str(jx1) + "," + str(jy1), 20, 0).split(",");
  jx1 = float(str1[0]);
  jy1 = float(str1[1]);

  //Горизонтальный слайдер; координаты x, y, ширина, высота, скругление, значение -50.. 50, работает ступенчато, цвет фона, цвет слайдера, текст - положение этого слайдера, размер текста 20, цвет 0 - черный;
  cx = sldx.update(width - 220, 50, 400, 50, 10, 50, -50, 50, true, #0000FF, #594BFF, str(cx), 20, 0);

  //Вертикальный слайдер; координаты x, y, ширина, высота, скругление, размер слайдера, значения -50.. 50, работает бесступенчато, цвет фона, цвет слайдера, текст - значение этого слайдера во float(ограничено 3 знаками после запятой с помощью nfc()), размер текста 20, цвет текста - красный;
  cy = sldy.update(width - 50, 300, 50, 400, 10, 50, -50, 50, false, #0000FF, #594BFF, nfc(cy, 3), 20, #FF0000);

  //Прямоугольная кнопка; координаты зависят от положения первого джойстика, шиирина, высота, скругление, цвет зависит от fn1, ширина обводки 10, цвет обводки красный, текст Фон, размер текста 160, цвет текста синий;
  if (rectButton(width / 2 - 200 + jx1, height / 2 - jy1, 400, 200, 20, fn1 ? #00FF00 : #00FFFF, 10, #FF0000, "Фон", 160, #0000FF) && !flr) {
    flr = true;
    fn = !fn;    //Меняет переменную fn
  }

  //Круглая кнопка; координаты зависят от положения двух слайдеров, диаметр 200, цвет, ширина обводки 10, цвет обводки, текст Цвет, размер текста 70, цвет текста;
  if (circleButton(width / 2 + 200 + cx, height / 2 - cy, 200, #00FFFF, 10, #FF0000, "Цвет", 70, #0000FF) && !flc) {
    flc = true;
    fn1 = !fn1;    //Меняет переменную fn1
  }
}

void mouseReleased() {    //Обнуление переменных при отпускании мыши
  flr = false;
  flc = false;
}
```


<a id="ver"></a>
## Версии
- v1.0
- v1.1 - добавление списка
- v1.2 - добавление текста, инвертирование показаний вертикального слайдера и джойстика в оси y
- v1.3 - исправление обводки у кнопок
